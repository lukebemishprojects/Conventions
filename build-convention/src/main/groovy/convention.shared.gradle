plugins {
	id 'groovy-gradle-plugin'
	id 'maven-publish'
	id 'dev.lukebemish.immaculate'
}

project.group = 'dev.lukebemish'+(':' + rootProject.name + project.path).split(':')[0..-2].join('.')

repositories {
	mavenCentral()
	gradlePluginPortal()
}

java.withSourcesJar()
java.toolchain.languageVersion.set JavaLanguageVersion.of(17)

processResources {
	from project.rootDir.toPath().resolve('LICENSE').toFile()
}

tasks.named('jar', Jar) {
	manifest {
		attributes(['Implementation-Version': project.version])
	}
}

immaculate {
	workflows.register('java') {
		java()
		trailingNewline()
		noTrailingSpaces()
		noTabs(4)
		googleFixImports()
		toggleOn.set 'CHECKSTYLE:ON'
		toggleOff.set 'CHECKSTYLE:OFF'
	}
	workflows.register('groovy') {
		def sourceDirs = files()
		sourceSets.configureEach { sourceDirs.from(it.getAllSource()) }
		files.from(sourceDirs.filter { it.name.endsWith '.groovy' })

		trailingNewline()
		noTrailingSpaces()
		noTabs(4)
	}
	workflows.register('packageInfo') {
		def sourceDirs = files()
		sourceSets.configureEach { sourceDirs.from(it.getAllSource()) }
		files.from(sourceDirs.filter { it.name == 'package-info.java' })

		custom('packageIsNullMarked') {
			if (!it.contains('@NullMarked')) {
				throw new GradleException('Packages should be null-marked')
			}
		}
	}
}
